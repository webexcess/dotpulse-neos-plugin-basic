prototype(Basic:ScriptCollection) < prototype(TS:Collection) {
	@context {
		VersionOfAssets = ${baseConfiguration.VersionOfAssets ? '?v=' + baseConfiguration.VersionOfAssets : ''}
	}
	collection = null
	itemName = 'item'
	itemRenderer = TS:Collection {
		collection = ${item ? String.split(String.replace(String.trim(item), ']', ''), '[') : []}
		iterationName = 'iterator'
		itemName = 'entry'
		itemRenderer = TS:Case {
			isFilename {
				condition = ${iterator.isFirst}
				renderer = TS:Case {
					isExternal {
						condition = ${String.indexOf(entry, '//') != -1}
						renderer = ${entry}
					}
					isInternal {
						condition = ${entry}
						renderer = TS:ResourceUri {
							path = ${'resource://' + sitePackage + '/Public/Scripts/' + entry +'.js'}
						}
						@process.wrap = ${value + VersionOfAssets}
					}
					@process.wrap = ${' src="' + value + '"'}
				}
			}
			areAttributes {
				condition = ${entry}
				renderer = ${' ' + String.trim(entry)}
			}
		}
		@process.wrap = ${value ? '<script' + value + '></script>' : ''}
	}
}
