prototype(Basic:StyleCollection) < prototype(TS:Collection) {
	@context {
		VersionOfAssets = ${baseConfiguration.VersionOfAssets ? '?v=' + baseConfiguration.VersionOfAssets : ''}
		LoadCSSAsynchron = ${baseConfiguration.LoadCSSAsynchron}
	}
	collection = null
	itemName = 'item'
	itemRenderer = TS:Collection {
		collection = ${item ? String.split(String.replace(String.trim(item), ']', ''), '[') : []}
		iterationName = 'iterator'
		itemName = 'entry'
		itemRenderer = TS:Case {
			isFilename {
				condition = ${iterator.isFirst}
				renderer = TS:Case {
					isExternal {
						condition = ${String.indexOf(entry, '//') != -1}
						renderer = ${entry}
					}
					isInternal {
						condition = ${entry}
						renderer = TS:ResourceUri {
							path = ${'resource://' + sitePackage + '/Public/Styles/' + entry +'.css'}
							@process.wrap = ${value + VersionOfAssets}
						}
					}
					@process.wrap = ${' href="' + value + '"'}
				}
			}
			areAttributes {
				condition = ${entry}
				renderer = ${' ' + String.trim(entry)}
			}
		}
		@process.wrap = ${value ? LoadCSSAsynchron ? '<link rel="preload"' + value + ' as="style" onload="this.rel=\'stylesheet\'" /><noscript><link rel="stylesheet"' + value + ' /></noscript>' : '<link rel="stylesheet"' + value + ' />' : ''}
	}
}
