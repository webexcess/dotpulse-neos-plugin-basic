prototype(Basic:StyleCollection) < prototype(Fusion:Collection) {
	@context.LoadCSSAsynchron = ${baseConfiguration.LoadCSSAsynchron}
	collection = null
	itemName = 'item'
	itemRenderer = Fusion:Collection {
		collection = ${item ? String.split(String.replace(String.trim(item), ']', ''), '[') : []}
		iterationName = 'iterator'
		itemName = 'entry'
		itemRenderer = Fusion:Case {
			isFilename {
				condition = ${iterator.isFirst}
				renderer = Fusion:Case {
					isExternal {
						condition = ${String.indexOf(entry, '//') != -1}
						renderer = ${entry}
					}
					isInternal {
						condition = ${entry}
						renderer = Fusion:ResourceUri {
							path = ${'resource://' + sitePackage + '/Public/Styles/' + entry +'.css'}
							@process.wrap = ${value + VersionOfAssets}
						}
					}
					@process.wrap = ${' href="' + value + '"'}
				}
			}
			areAttributes {
				condition = ${entry}
				renderer = ${' ' + String.trim(entry)}
			}
		}
		@process.wrap = ${value ? LoadCSSAsynchron ? '<link rel="preload"' + value + ' as="style" onload="this.rel=\'stylesheet\'" /><noscript><link rel="stylesheet"' + value + ' /></noscript>' : '<link rel="stylesheet"' + value + ' />' : ''}
	}
}
