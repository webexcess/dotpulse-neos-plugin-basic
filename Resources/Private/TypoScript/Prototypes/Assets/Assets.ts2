prototype(Basic:Asset) < prototype(TS:Value) {
	// General || Live || Backend
	context = 'General'
	// Body || Head
	location = 'Body'

	@context {
		context = ${this.context}
		location = ${this.location}
	}

	value = TS:Array {
		styles = Basic:StyleCollection {
			collection = ${String.split(baseConfiguration[context][location].Style, ',')}
		}
		scripts = Basic:ScriptCollection {
			collection = ${String.split(baseConfiguration[context][location].Script, ',')}
		}
	}

	@if.hasContext = TS:Case {
		inBackend {
			condition = ${context == 'Backend'}
			renderer = ${node.context.inBackend}
		}

		isLive {
			condition = ${context == 'Live'}
			renderer = ${node.context.live}
		}

		default {
			condition = true
			renderer = true
		}
	}
}

prototype(Basic:Assets)  < prototype(TS:Value) {
	// Body || Head
	location = 'Body'
	@context.location = ${this.location}

	value = TS:Array {
		general = Basic:Asset {
			context = 'General'
			location = ${location}
		}
		inBackend = Basic:Asset {
			context = 'Backend'
			location = ${location}
		}
		live = Basic:Asset {
			context = 'Live'
			location = ${location}
		}
	}

	@position = 'start'
}
