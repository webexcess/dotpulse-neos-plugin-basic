prototype(Basic:MenuLink) < prototype(Fusion:Tag) {
	@context {
		targetSchema = Fusion:Case {
			isShortcutWithTargetNode {
				condition = ${q(node).is('[instanceof Neos.Neos:Shortcut]') && q(node).property('targetMode') == 'selectedTarget'}
				renderer = Fusion:Value {
					scheme = ${Neos.Link.getScheme(q(node).property('target'))}
					value = ${this.scheme ? this.scheme : 'external'}
				}
			}
			default {
				condition = true
				renderer = 'node'
			}
		}
		href = NodeUri {
			node = ${node}
		}
	}
	tagName = 'a'
	attributes {
		rel = ${targetSchema == 'node' ? false : 'noopener'}
		target = ${targetSchema == 'node' || String.startsWith(href, 'mailto:') ? false : '_blank'}
		class = Fusion:RawArray {
			targetSchema = ${'nav-' + targetSchema}
		}
		href = ${href}
	}
	content = ${q(node).property('title')}
}
