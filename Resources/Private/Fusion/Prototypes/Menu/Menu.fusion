prototype(Basic:Menu) < prototype(Menu) {
	@context {
		entryLevel = ${this.entryLevel}
		instanceFilter = ${this.instanceFilter}
	}

	templatePath = ${'resource://' + basePackage + '/Private/Templates/TypoScriptObjects/Menu.html'}
	sectionName = 'Menu'
	filter = 'Neos.Neos:Document,!Dotpulse.Basic:NotInMenu'
	instanceFilter = ${String.replace('[instanceof ' + Array.join(String.split(this.filter, ','), '][instanceof ') + ']', 'instanceof !', '!instanceof ')}

	entryLevel = 1
	maximumLevels = 1

	attributes {
		class = Fusion:RawArray {
			entryLevel = ${'nav-' + entryLevel}
		}
	}

	prepend = ''
	append = ''

	site = ${site}

	normal.attributes.class = Fusion:RawArray {
		item = 'nav-item'
		hasSubpages = ${q(item.node).children(instanceFilter).is() ? 'nav-item-has-subpages' : false}
	}

	active.attributes.class = Fusion:RawArray {
		item = 'nav-item'
		active = 'active'
		hasSubpages = ${q(item.node).children(instanceFilter).is() ? 'nav-item-has-subpages' : false}
	}

	current.attributes.class = Fusion:RawArray {
		item = 'nav-item'
		current = 'current'
		active = 'active'
		hasSubpages = ${q(item.node).children(instanceFilter).is() ? 'nav-item-has-subpages' : false}
	}

	link = Basic:MenuLink

	@process.shy = Basic:AlwaysReplaceWithShy
}
